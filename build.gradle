import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'io.github.apickledwalrus'
version '1.3-alpha3'

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    implementation group: 'org.jetbrains', name: 'annotations', version: '16.0.2'
    implementation group: 'com.google.code.findbugs', name: 'findbugs', version: '3.0.1'
    shadow group: 'org.ow2.asm', name: 'asm', version: '9.1' // For CraftRemapper TODO see if we can access this from the server

    implementation group: 'org.spigotmc', name: 'spigot-api', version: '1.16.5-R0.1-SNAPSHOT'
    implementation (group: 'com.github.SkriptLang.Skript', name: 'Skript', version: '2.6-alpha1') {
        transitive = false
    }
}

task jar(overwrite: true, type: ShadowJar) {
    from sourceSets.main.output
}

task build(overwrite: true, type: ShadowJar) {
    from sourceSets.main.output
}

task nightlyBuild(type: ShadowJar) {
    from sourceSets.main.output
    archiveFileName = 'skript-gui-nightly.jar'
}

tasks.withType(ShadowJar) {
    configurations = [
            project.configurations.shadow
    ]
    relocate 'org.objectweb.asm', 'io.github.apickledwalrus.skriptgui.dependencies.asm'
    minimize()
}

compileJava { // Suppress the warnings generated by CraftRemapper (for now, we know it could be removed)
    options.compilerArgs << '-XDignore.symbol.file'
    options.fork = true
    options.forkOptions.executable = 'javac'
}
